// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.10.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'


repositories {
    jcenter()
    maven {
        url project.hasProperty('ligisMavenUrl') ? ligisMavenUrl : "https://raw.github.com/ligi/ligis-mvn-repo/master";
    }
}

android {
    compileSdkVersion 20
    buildToolsVersion "19.1.0"

    defaultConfig {
        applicationId "org.ligi.ajsha"
        minSdkVersion 9
        targetSdkVersion 20
        versionCode 4
        versionName "0.4"
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"

    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                def file = variant.outputFile
                variant.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
            }

        }

    }
    signingConfigs {
        release {
            storeFile file("/home/ligi/.android/ajsha.keystore")
        }
    }

    // hack for instrumentation testing :-(
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    flavorDimensions "analytics"

    productFlavors {

        withAnalytics {
            flavorDimension "analytics"
        }

        noAnalytics {
            flavorDimension "analytics"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        ignore  "InvalidPackage"
    }

}

dependencies {
    compile 'com.google.guava:guava:17.0'
    compile 'org.beanshell:bsh:2.0b4'
    compile 'com.jakewharton:butterknife:5.1.1'
    compile 'org.ligi:AXT:0.28'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'com.android.support:appcompat-v7:20.0.+'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0.2+@aar'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'org.ligi:tracedroid:1.1'

    def play_services = 'com.google.android.gms:play-services:5.0.89'

    withAnalyticsCompile play_services

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.1'
    androidTestCompile 'com.squareup:fest-android:1.0.7'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.guava'
    }

    androidTestCompile('com.jakewharton.espresso:espresso-support-v4:1.1-r3') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.guava'
    }

}
