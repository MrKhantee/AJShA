buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:1.0.3') {
            exclude module: 'guava'
        }
        classpath 'com.github.nrudenko:gradle-android-cq-plugin:0.1.5'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'android-cq'

repositories {
    jcenter()
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.ligi.ajsha"
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 12
        versionName "0.12"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "AJSHA-$versionName"
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // hack for instrumentation testing :-(
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    flavorDimensions "analytics", "distribution"

    productFlavors {

        withAnalytics {
            dimension "analytics"
        }

        noAnalytics {
            dimension "analytics"
        }

        forPlay {
            dimension "distribution"
        }

        forFDroid {
            dimension "distribution"
        }

    }

    // needed for assertJ
    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
    }

    lintOptions {
        ignore "InvalidPackage"
    }

}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'org.beanshell:bsh:2.0b5'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'org.ligi:AXT:0.33'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'com.android.support:appcompat-v7:22.2.0'

    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'org.ligi:tracedroid:1.4'

    forPlayCompile 'fr.nicolaspomepuy:discreetapprate:2.0.4'

    def play_services = 'com.google.android.gms:play-services-base:6.5.+'

    withAnalyticsCompile play_services

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.10'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'

    // have to wait with update till this is solved: https://code.google.com/p/dexmaker/issues/detail?id=40
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'

    androidTestCompile 'com.android.support:appcompat-v7:22.2.0'

}
