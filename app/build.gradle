buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.2'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:1.0.0') {
           exclude module: 'guava'
        }
        classpath 'com.github.nrudenko:gradle-android-cq-plugin:0.1.5'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'android-cq'

repositories {
    jcenter()
}

android {
    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        applicationId "org.ligi.ajsha"
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 12
        versionName "0.12"
   	testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    def replace = file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk")
                    output.outputFile = new File(file.parent, replace)
                }
            }

        }
    }

    signingConfigs {
        release {
            storeFile file("/home/ligi/.android/ajsha.keystore")
        }
    }

    // hack for instrumentation testing :-(
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    flavorDimensions "analytics", "distribution"

    productFlavors {

        withAnalytics {
            flavorDimension "analytics"
        }

        noAnalytics {
            flavorDimension "analytics"
        }

        forPlay {
            flavorDimension "distribution"
        }

        forFDroid {
            flavorDimension "distribution"
        }

    }

    // needed for assertJ
    packagingOptions {
      exclude 'asm-license.txt'
      exclude 'LICENSE'
      exclude 'NOTICE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        ignore "InvalidPackage"
    }

}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'org.beanshell:bsh:2.0b5'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'org.ligi:AXT:0.33'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'com.android.support:appcompat-v7:22.0.0'

    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'org.ligi:tracedroid:1.4'

    forPlayCompile 'fr.nicolaspomepuy:discreetapprate:2.0.4'

    def play_services = 'com.google.android.gms:play-services-base:6.5.+'

    withAnalyticsCompile play_services

    androidTestCompile 'org.boundbox:boundbox-library:1.2.0'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.7'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'

    androidTestCompile 'com.android.support:appcompat-v7:22.0.0'

}
